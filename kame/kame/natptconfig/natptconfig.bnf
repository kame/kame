/*
 * Copyright (C) 1995, 1996, 1997, and 1998 WIDE Project.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. Neither the name of the project nor the names of its contributors
 *    may be used to endorse or promote products derived from this software
 *    without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE PROJECT AND CONTRIBUTORS ``AS IS'' AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE PROJECT OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 *
 *	$Id: natptconfig.bnf,v 1.3 2000/02/18 11:39:54 fujisawa Exp $
 */

/* Top level definitions							*/

      <statement> ::= <comment>
		    | <question>
		    | <break>
		    | <interface>
		    | <prefix>
		    | <rule>
		    | <switch>
		    | <set>
		    | <show>
		    | <error>


/* Comment definition								*/

	<comment> ::= SCOMMENT


/* ???										*/

       <question> ::= "?"


/* Stop at breakpoiont (for debug only)						*/

	  <break> ::= SBREAK


/* Interface definitions							*/

      <interface> ::= "interface" "?"
		    | "interface" <netdevice> <in_ex>

	  <in_ex> ::= "internal"
		    | "external"


/* Set faith/NATPT prefix to the kernel						*/

	 <prefix> ::= "prefix" "?"
		    | "faith" IPV6ADDR
		    | "faith" IPV6ADDR "/" DECIMAL
		    | "natpt" IPV6ADDR
		    | "natpt" IPV6ADDR "/" DECIMAL


/* Tranlation rule definitions							*/

	   <rule> ::= "map" "?"
		    | "map" <dir> "from" <ipaddrport> "to" <ipaddrport>
		    | "map" <dir> "from" "any4" "to" <ipaddrport>
		    | "map" <dir> "from" "any6" "to" <ipaddrport>
		    | "map" "from" "any6" "to" "faith"
		    | "map" "from" <ipaddrport> "to" "faith"
		    | "map" "flush" <opt_type>


	    <dir> ::= "inbound"
		    | "incoming"
		    | "outbound"
		    | "outgoing"


/* Translation on/off switch definition						*/

	  <swich> ::= "map" "enable"
		    | "map" "disable"


/* Set definition								*/

	    <set> ::= "set" "?"
		    | "set" <name> "=" <decimail>
		    | "set" <name> "=" <hexdecimal>


/* Show definitions								*/

	   <show> ::= "show" "?"
		    | "show" "interface" <opt_netdevice>
		    | "show" "prefix"
		    | "show" "static"
		    | "show" "dynamic"
		    | "show" "xlate" <opt_decimal>
		    | "show" "variables"
		    | "show" "mapping"


/* Miscellaneous definitions							*/

  <opt_netdevice> ::= eps
		    | <netdevice>

      <netdevice> ::= STRING
		    | NAME

      ipaddrport> ::= <ipv4addrs>
		    | <ipv4addrs> <port>
		    | <ipv6addrs>
		    | <ipv6addrs> <port>

      <ipv4addrs> ::= <ipv4addr>
		    | <ipv4addr> "/" <decimal>
		    | <ipv4addr> "-" <ipv4addr>

      <ipv6addrs> ::= <ipv6addr>
		    | <ipv6addr> "/" <decimal>

       <ipv4addr> ::= IPV4ADDR

       <ipv6addr> ::= IPV6ADDR

	   <name> ::= SNAME

	<decimal> ::= SDECIMAL

     <hexdecimal> ::= SHEXDECIMAL

	   <port> ::= "port" SDECIMAL
		    | "port" SDECIMAL ":" SDECIMAL
		    | "port" SDECIMAL "-" SDECIMAL


/* Optional termila definitions							*/

       <opt_type> ::= eps
		    | "static"
		    | "dynamic"

    <opt_decimal> ::= eps
		    | DECIMAL
