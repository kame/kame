dnl Process this file with autoconf to produce a configure script.
AC_INIT(v6test.c)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_CANONICAL_HOST

AC_SUBST(OPTFLAGS)
AC_SUBST(LIBFLAGS)

v6type=unknown

AC_CHECK_TYPE(uint8_t, u_int8_t)
AC_CHECK_TYPE(uint16_t, u_int16_t)
AC_CHECK_TYPE(uint32_t, u_int32_t)
dnl Guess from defs
AC_MSG_CHECKING(for v6 stack type from defines)
if test "$v6type" = "unknown"; then
	AC_EGREP_CPP(yes,
	[#include <sys/param.h>
	#ifdef _HITACHI_INET6
	yes
	#endif
	], [v6type=hitachi])
fi
if test "$v6type" = "unknown"; then
	AC_EGREP_CPP(yes,
	[#include <sys/param.h>
	#ifdef _TOSHIBA_INET6
	yes
	#endif
	], [v6type=toshiba])
fi
if test "$v6type" = "unknown"; then
	AC_EGREP_CPP(yes,
	[#include <sys/param.h>
	#ifdef _ZETA_MINAMI_INET6
	yes
	#endif
	], [v6type=zeta])
fi
AC_MSG_RESULT($v6type)

dnl Guess from os type
if test "$v6type" = "unknown"; then
	AC_MSG_CHECKING(for v6 stack type from OS/files)
	case "$host_os" in
	bsdi*|freebsd2*|netbsd*)
		if test -f /usr/include/netinet6/in6_ifattach.h; then
			v6type=kame
		fi
	esac
	AC_MSG_RESULT($v6type)
fi

case "$v6type" in
unknown)
		echo "could not guess the v6 stack type. use local include files" ;
		OPTFLAGS="-I./include $OPTFLAGS" ;;
*)
		echo "$v6type seems to support RFC2292(adv-api)." ;;
esac

AC_MSG_CHECKING(if the stack needs -DINET6)
AC_CACHE_VAL(ts1_cv_ipv6, [dnl
AC_TRY_COMPILE([#define INET6
#include <sys/types.h>
#include <netinet/in.h>],
        [int x = IPPROTO_IPV6; struct in6_addr a;],
        [ts1_cv_ipv6="yes"], [ts1_cv_ipv6="no"])])
AC_CACHE_VAL(ts2_cv_ipv6, [dnl
AC_TRY_COMPILE([
#include <sys/types.h>
#include <netinet/in.h>],
        [int x = IPPROTO_IPV6; struct in6_addr a;],
        [ts2_cv_ipv6="yes"], [ts2_cv_ipv6="no"])])
if test "$ts1_cv_ipv6" = yes -a "$ts2_cv_ipv6" = no; then
        OPTFLAGS="-DINET6 $OPTFLAGS"
	AC_MSG_RESULT("you need '-DINET6'")
else
	AC_MSG_RESULT("no")
fi

AC_MSG_CHECKING(for libresolv)
AC_CACHE_VAL(ts_cv_ntop, [dnl
AC_TRY_LINK([#include <sys/types.h>
#include <sys/socket.h>],
	[char *a, *b; inet_ntop(AF_INET6, a, b)],
	[ts_cv_ntop="yes"], [ts_cv_ntop="no"])])
AC_MSG_RESULT($ts_cv_ntop)
if test "$ts_cv_ntop" = no; then
	LIBFLAGS="-lresolv $LIBFLAGS"
fi

dnl
dnl check for pcap library
dnl LIBFLAGS=-L/usr/local/v6/lib
AC_MSG_CHECKING(if --with-pcap option is specified)
AC_ARG_WITH(pcap, [  --with-pcap=DIR       specify pcap directory],
	[pcap_dir=$withval])
AC_MSG_RESULT(${pcap_dir-"default"})

AC_MSG_CHECKING(for libpcap)
pcap_lib=no
pcap_include=no
for dir in $pcap_dir /usr/local/v6 /usr/local /usr/pkg /usr; do
	if test -d $dir -a -f $dir/libpcap.a; then
		pcap_lib="$dir"
	else
		if test -d $dir/lib -a -f $dir/lib/libpcap.a; then
			pcap_lib="$dir/lib"
		fi
	fi
	if test -d $dir -a -f $dir/pcap.h; then
		pcap_include="$dir"
	else
		if test -d $dir/include -a -f $dir/include/pcap.h; then
			pcap_include="$dir/include"
		fi
	fi
	if test "$pcap_lib" != "no" -a "$pcap_include" != "no"; then
		LDFLAGS="-L$pcap_lib $LDFLAGS"
		CPPFLAGS="-I$pcap_include $CPPFLAGS"
		AC_MSG_RESULT("$pcap_lib and $pcap_include")
		AC_CHECK_LIB(pcap, pcap_lookupdev)
		break;
	fi
done

AC_OUTPUT(Makefile)
